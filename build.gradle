/////////////////////////////
// BuildScript and Plugins //
/////////////////////////////

buildscript {
    repositories {
        jcenter()
        maven {
            name 'forge'
            url 'http://files.minecraftforge.net/maven'
        }
    }
    dependencies {
        classpath(group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true)
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version "1.3.11"
    id "com.github.hierynomus.license" version "0.15.0"
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

apply plugin: 'net.minecraftforge.gradle'

//////////////////
// Dependencies //
//////////////////

repositories {
    mavenCentral()
    maven {
        url 'https://files.minecraftforge.net/maven'
    }
    maven {
        url 'https://minecraft.curseforge.com/api/maven/'
    }
}

configurations {
    embed
    compile.extendsFrom embed
}

dependencies {
    minecraft group: 'net.minecraftforge', name: 'forge', 'version': "$minecraft_version-$forge_version"

    compile group: 'kottle', name: 'Kottle', 'version': kottle_version

    embed group: 'com.googlecode.soundlibs', name: 'mp3spi', version: '1.9.5-1'
    embed group: 'com.googlecode.soundlibs', name: 'vorbisspi', version: '1.0.3.3'
}

minecraft {
    mappings channel: 'snapshot', version: forge_mappings

    runs {
        client {
            properties 'forge.logging.console.level': 'debug'
            properties 'fml.earlyprogresswindow': 'false'
            workingDirectory project.file('run')
            mods {
                betterrecords {
                    source sourceSets.main
                }
            }
        }
        server {
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run')
            mods {
                betterrecords {
                    source sourceSets.main
                }
            }
        }
    }
}

tasks {
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}

///////////////
// Artifacts //
///////////////

version = "$minecraft_version-$mod_version"

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'
        expand 'version':project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

shadowJar {
    classifier '' // Replace the default jar

    configurations = [project.configurations.embed]

    relocate 'com.jcraft', 'tech.feldman.betterrecords.repack.jcraft'
    relocate 'javazoom', 'tech.feldman.betterrecords.repack.javazoom'
    relocate 'org.tritonus', 'tech.feldman.betterrecords.repack.tritonus'
}

tasks.build.dependsOn shadowJar

reobf {
    shadowJar {}
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives deobfJar
    archives sourcesJar
    archives javadocJar
}

///////////////
// Licencing //
///////////////

license {
    include "**/*.kt"
}
